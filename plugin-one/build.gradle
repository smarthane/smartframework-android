apply plugin: 'com.android.application'
apply plugin: 'com.didi.virtualapk.plugin'
android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    defaultConfig {
        applicationId "com.smarthane.plugin.one"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':lib-core')
}
//def VATagetHost = "../app"
def VATagetHost = "app"
def dirRelease = "${rootDir}/build/plugin/release"
virtualApk {
    packageId = 0x6f             // The package id of Resources.
    targetHost = VATagetHost // The path of application module in host project. 单独编译路径为：  ../app  批量打包编译为：app
    applyHostMapping = true      // [Optional] Default value is true.
}
assemblePlugin.doFirst {
    delete dirRelease
}
assemblePlugin.doLast {
    File targetDir = new File(dirRelease)
    if(!targetDir.exists()) {
        targetDir.mkdirs()
    }
    FileTree tree = fileTree(dir: '/build/outputs/plugin/release', include: '*.apk')
    tree.each {File file ->
        copy {
            from("/build/outputs/plugin/release/"+file.name)
            into(dirRelease)
            rename(file.name,"plugin_"+android.defaultConfig.applicationId+"_v"+android.defaultConfig.versionName+".apk")
        }
    }
}